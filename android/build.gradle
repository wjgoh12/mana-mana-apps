buildscript {
    ext.kotlin_version ='2.1.0'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.huawei.agconnect:agcp:1.6.0.300'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
        maven { url 'https://s01.oss.sonatype.org/content/groups/public' }
        maven { url 'https://repo1.maven.org/maven2/' }
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    }
}

// Configure Flutter properties for plugins
ext {
    flutter = [
        compileSdkVersion: 36,
        minSdkVersion: 21,
        targetSdkVersion: 35
    ]
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    // Namespace Fix for AGP 8+
    project.afterEvaluate {
        def android = project.extensions.findByName("android")
        if (android != null) {
            if (android.namespace == null) {
                 if (project.name == 'huawei_hmsavailability') {
                     android.namespace = 'com.huawei.hms.flutter.hmsavailability'
                 } else if (project.name == 'huawei_account') {
                     android.namespace = 'com.huawei.hms.flutter.account'
                 } else {
                     android.namespace = project.group
                 }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}